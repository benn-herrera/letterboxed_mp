set(TARGET bng)
set(LIB_TYPE SHARED)

set(ADDITIONAL_HEADERS ${MOBILE_COMMON_HEADERS} ${API_HEADERS})
set(ADDITIONAL_SOURCES ${MOBILE_COMMON_SOURCES})

include("${CMAKE_INCLUDE}/target_lib.cmake")

bng_add_link_libraries(
    core
    engine
    "-framework Foundation"
)

# not used included in C++ implementation files. solely for
# framework creation by xcode
set(FRAMEWORK_UMBRELLA_HEADER ${TARGET}.h)

set(BUNDLE_ID com.tinybitsinteractive.${TARGET})
set(BUNDLE_VERSION 0.5.0)

if(NOT BNG_APPLE_DEV_TEAM)
  set(BNG_APPLE_DEV_TEAM $ENV{BNG_APPLE_DEV_TEAM})
endif()

set_target_properties(${TARGET} PROPERTIES
  FRAMEWORK TRUE
  MACOSX_FRAMEWORK TRUE
  MACOSX_BUNDLE TRUE
  MACOSX_FRAMEWORK_IDENTIFIER ${BUNDLE_ID}  
  MACOSX_FRAMEWORK_SHORT_VERSION_STRING "${BUNDLE_VERSION}"
  MACOSX_FRAMEWORK_BUNDLE_VERSION ${BUNDLE_VERSION}
  VERSION ${BUNDLE_VERSION}
  SOVERSION ${BUNDLE_VERSION}
  PUBLIC_HEADER ${FRAMEWORK_UMBRELLA_HEADER}

  XCODE_ATTRIBUTE_PRODUCT_BUNDLE_IDENTIFIER ${BUNDLE_ID}
  XCODE_ATTRIBUTE_DYLIB_INSTALL_NAME_BASE "@rpath"
  XCODE_ATTRIBUTE_DEVELOPMENT_TEAM ${BNG_APPLE_DEV_TEAM}
  #XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY "iPhone Developer"  
  XCODE_ATTRIBUTE_ONLY_ACTIVE_ARCH NO
  XCODE_ATTRIBUTE_DEFINES_MODULE YES  
  )



set_source_files_properties(
  ${FRAMEWORK_UMBRELLA_HEADER} ${API_HEADERS}
  PROPERTIES
  MACOSX_PACKAGE_LOCATION Headers
  )

if (SWIFT_BRIDGE_HEADER_DIR)
  set(SWIFT_BRIDGE_HEADER_DIR "${SWIFT_BRIDGE_HEADER_DIR}" CACHE STRING "")
  install(FILES
      ${FRAMEWORK_UMBRELLA_HEADER} ${API_HEADERS}  
      DESTINATION "${SWIFT_BRIDGE_HEADER_DIR}")
else()
  message(FATAL_ERROR "SWIFT_BRIDGE_HEADER_DIR NOT DEFINED")
endif()

set(SWIFT_BRIDGE_HEADER_DIR "${SWIFT_BRIDGE_HEADER_DIR}" CACHE PATH "")
set(SWIFT_BRIDGE_SWIFT_DIR "${SWIFT_BRIDGE_SWIFT_DIR}" CACHE PATH "")
if (NOT (SWIFT_BRIDGE_HEADER_DIR AND SWIFT_BRIDGE_SWIFT_DIR))
    message(FATAL_ERROR 
        "SWIFT_BRIDGE_HEADER_DIR and SWIFT_BRIDGE_SWIFT_DIR must be defined via -D<VAR>=<VAL> argument to cmake from build_swift_xcf.sh"
      )
endif()

set(GEN_API_SWIFT_H_NAME "${GEN_API_NAME}_swift.h")
set(GEN_API_SWIFT_H "${GEN_OUT_DIR}/${GEN_API_SWIFT_H_NAME}")
set(GEN_API_SWIFT_CPP "${GEN_OUT_DIR}/${GEN_API_NAME}_swift.cpp")
set(GEN_API_SWIFT "${SWIFT_BRIDGE_SWIFT_DIR}/${GEN_API_NAME}.swift")
add_custom_command(
    OUTPUT "${GEN_SWIFT_H}" "${GEN_SWIFT_CPP}"
    COMMAND "${Python_EXECUTABLE}" "${GenApiSources_SCRIPT}"
        generate-swift-binding --api-def="${API_DEF}" --api-h="${GEN_API_H}"
        --out-h="${GEN_SWIFT_H}" --out-cpp="${GEN_SWIFT_CPP}"
    MAIN_DEPENDENCY "${API_DEF}"
    DEPENDS "${GenApiSources_SCRIPT}"
    WORKING_DIRECTORY "${PROJECT_BINARY_DIR}"
)
add_custom_command(
    OUTPUT "${GEN_API_SWIFT}"
    COMMAND "${Python_EXECUTABLE}" "${GenApiSources_SCRIPT}"
        generate-swift-wrapper --api-def="${API_DEF}" --swift-h="${GEN_API_SWIFT_H_NAME}"
        --out-swift="${GEN_API_SWIFT}"
    MAIN_DEPENDENCY "${API_DEF}"
    DEPENDS "${GenApiSources_SCRIPT}"
    WORKING_DIRECTORY "${PROJECT_BINARY_DIR}"
)


set(TARGET bng)
set(LIB_TYPE SHARED)
set(ADDITIONAL_HEADERS ${MOBILE_COMMON_HEADERS} ${API_HEADERS} ${GEN_SWIFT_H} ${GEN_API_SWIFT})
set(ADDITIONAL_SOURCES ${MOBILE_COMMON_SOURCES} ${GEN_SWIFT_CPP})

include("${CMAKE_INCLUDE}/target_lib.cmake")

set_source_files_properties(
  "${GEN_SWIFT_H}" "${GEN_SWIFT_CPP}" "${GEN_API_SWIFT}"
  PROPERTIES
  GENERATED TRUE)

bng_include_directories(PRIVATE "${SWIFT_BRIDGE_HEADER_DIR}")

bng_add_link_libraries(
    core
    engine
    "-framework Foundation"
)

# not used included in C++ implementation files. solely for
# framework creation by xcode
set(FRAMEWORK_UMBRELLA_HEADER "${GEN_OUT_DIR}/${TARGET}.h")

set(BUNDLE_ID com.tinybitsinteractive.${TARGET})
set(BUNDLE_VERSION 0.5.0)

set_target_properties(${TARGET} PROPERTIES
  FRAMEWORK TRUE
  MACOSX_FRAMEWORK TRUE
  MACOSX_BUNDLE TRUE
  MACOSX_FRAMEWORK_IDENTIFIER ${BUNDLE_ID}  
  MACOSX_FRAMEWORK_SHORT_VERSION_STRING "${BUNDLE_VERSION}"
  MACOSX_FRAMEWORK_BUNDLE_VERSION ${BUNDLE_VERSION}
  VERSION ${BUNDLE_VERSION}
  SOVERSION ${BUNDLE_VERSION}
  PUBLIC_HEADER ${FRAMEWORK_UMBRELLA_HEADER}

  XCODE_ATTRIBUTE_PRODUCT_BUNDLE_IDENTIFIER ${BUNDLE_ID}
  XCODE_ATTRIBUTE_DYLIB_INSTALL_NAME_BASE "@rpath"
  XCODE_ATTRIBUTE_DEVELOPMENT_TEAM ${BNG_APPLE_DEV_TEAM_ID}
  #XCODE_ATTRIBUTE_CODE_SIGN_IDENTITY "iPhone Developer"  
  XCODE_ATTRIBUTE_ONLY_ACTIVE_ARCH NO
  XCODE_ATTRIBUTE_DEFINES_MODULE YES  
  )

set_source_files_properties(
  ${FRAMEWORK_UMBRELLA_HEADER} ${API_HEADERS}
  PROPERTIES
  MACOSX_PACKAGE_LOCATION Headers
  )

install(FILES
    ${FRAMEWORK_UMBRELLA_HEADER} ${API_HEADERS}
    DESTINATION "${SWIFT_BRIDGE_HEADER_DIR}")
